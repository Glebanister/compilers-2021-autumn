-- Expression evaluator

import List;
import State;
import World;
import Lexer;
import Parser;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun checkVar (state, name) {
  case state.lookup (name) of
    Var (_) -> skip
  | _       -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun checkFun (state, name) {
  case state.lookup (name) of
    Fun (_, _) -> skip
  | _          -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  fun addZeroValue (s, name) {
--    printf ("addZeroValue(s: %s, name: %s)\n", s.string, name.string);
    addName (s, name, Var (0))
  }
--  printf ("addNames(state: %s, names: %s)\n", state.string, names.string);
  foldl (addZeroValue, state, names)
}

fun addNamesWithValues (state, names, values) {
  foldl (
    fun (s, [n, v]) { s.addName (n, v) },
    state,
    zip (names, values)
  )
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}

fun addDef (state, def) {
  case def of
    Var (names)            -> state.addNames (names)
  | Fun (name, args, body) -> state.addFunction (name, args, body)
  esac
}

fun addDefs (state, defs) {
  foldl (addDef, state, defs)
}

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[st, world], expr) {
  case (expr) of
    Assn (e1, e2) -> 
      case eval (c, e1) of
        [c, s] ->
          case eval (c, e2) of
            [[st, world], n] -> [[st <- [s, n], world], n]
          esac
      esac
  | Seq (e1, e2) ->
      case eval (c, e1) of
        [c, _] -> eval (c, e2)
      esac
  | Skip -> [c, None]
  | Read (s) -> 
      case readWorld (world) of
        [n, world] -> [[st <- [s, n], world], None]
      esac
  | Write (e) ->
      case eval (c, e) of
        [[st, world], v] -> [[st, writeWorld (v, world)], None]
      esac
  | If (cond, bt, bf) ->
      case eval (c, cond) of
        [c, v] -> if v then eval (c, bt) else eval (c, bf) fi
      esac
  | While (e, s) ->
      case eval (c, e) of
        [c, v] -> if v then eval (c, Seq (s, While (e, s))) else [c, None] fi
      esac
  | DoWhile (b, cond) -> eval (c, Seq (b, While (cond, b)))
  | Var (s) -> [c, st.lookup (s)]
  | Ref (s) -> [c, s]
  | Const (n) -> [c, n]
  | Ignore (e) -> eval (c, e)
  | Binop (o, e1, e2) ->
      case eval (c, e1) of
        [c, v1] ->
          case eval (c, e2) of
            [c, v2] -> [c, evalOp (o, v1, v2)]
          esac
      esac
  | Call (fname, args) ->
      st.checkFun(fname);
      case st.lookup (fname) of
        Fun (argNames, body) ->
          case evalList (c, args) of
            [[st, world], argValues] ->
              case eval ([addNamesWithValues (enterFunction (st), argNames, argValues), world], body) of
                [[st, world], ret] ->
                  [[c[0].leaveFunction (st.getGlobal), world], ret]
              esac
          esac
       | _ -> failure ("%s is not a name of function", fname)
      esac
  | Scope (defs, expr) ->
      case st.enterScope.addDefs (defs) of
        stWithDefs ->
          case eval ([stWithDefs, world], expr) of
            [[stAfterScope, wAfterScope], ret] -> [[stAfterScope.leaveScope, wAfterScope], ret]
          esac
      esac
  esac
}
(* End *)

-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState (), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}